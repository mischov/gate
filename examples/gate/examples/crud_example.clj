(ns gate.examples.crud-example
  (:require [gate :refer [defrouter handler]]))

;; By representing routes as Clojure data structures, Gate makes
;; it exceedingly simple to programatically generate routes.
;;
;; This example shows how one might go about using Gate to generate
;; routes similar to those generated by Rails's `resources`.

(defn keywordify
  "Transforms a string into a lowercase keyword"
  [s]
  (keyword (clojure.string/lower-case s)))

;; Generators for faux-handlers representing various CRUD actions.

(defn resource-index
  [resource-type]
  (handler []
    (str "Showing index of all " resource-type)))

(defn new-resource-form
  [resource-type]
  (handler []
    (str "Form for creating new" resource-type)))

(defn create-resource
  [resource-type]
  (handler []
    (str "Created resource of type " resource-type)))

(defn show-resource
  [resource-type]
  (handler [id]
    (str "Showing resource " resource-type "/" id)))

(defn edit-resource-form
  [resource-type]
  (handler [id]
    (str "Showing form to edit resource " resource-type "/" id)))

(defn update-resource
  [resource-type]
  (handler [id]
    (str "Updated resource " resource-type "/" id)))

(defn destroy-resource
  [resource-type]
  (handler [id]
    (str "Deleted resource " resource-type "/" id)))

;; Function to generate the routes.

(defn resources
  "Generates routes mimicking those generated by
   Rails's 'resources'."
  [resource-type]
  {:path (str "/" resource-type)
   :get (resource-index resource-type)
   :post (create-resource resource-type)
   :children [
              {:path "/new"
               :get (new-resource-form resource-type)}
              {:path "/:id"
               :get (show-resource resource-type)
               :put (update-resource resource-type)
               :delete (destroy-resource resource-type)
               :children [
                          {:path "/edit"
                           :get (edit-resource-form resource-type)}]}]})

;; Create an app with resources for 'posts' and 'users'

(defrouter app
  [(resources "posts")
   (resources "users")])

;; Go ahead and test app out.

;; (app {:uri "/posts" :request-method :get})
;; => {:status 200, :headers {"Content-Type" "text/html; charset=utf-8"}, :body "Showing index of all posts"}

;; (app {:uri "/posts/385783 :request-method :put})
;; => {:status 200, :headers {"Content-Type" "text/html; charset=utf-8"}, :body "Updated resource posts/385783"}
